'NOTE: To build on Win32 GCC, you need to go to the Makefile (i.e. glfw3/gcc_winnt/Makefile)
'and add -lole32 to the LDLIBS property

Strict

'Config settings
#GLFW_WINDOW_TITLE="Vortex2 Font Tool"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2
#OPENGL_DEPTH_BUFFER_ENABLED=True
'#GLFW_GCC_MSIZE_LINUX="32"
#If HOST = "winnt"
#BINARY_FILES += "*.exe"
#End

'Imports
Import mojo.app
Import mojo.input
Import src_tools.dialog
Import src_tools.fonttool_gui
Import vortex

Class FontToolApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(30)
		SetSwapInterval(1)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init()
			Notify "Error", Stats.ShaderError(), True
			EndApp()
		End
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		Print "Shader compilation: " + Stats.ShaderError()
		
		'Create gui
		mGui = New Gui
		
		Return False
	End
	
	Method OnUpdate:Int()
		'Update world
		World.Update()
	
		'Update GUI
		Local newFont:Font = mGui.Update(mFont)
		If newFont <> Null
			If mFont Then mFont.Discard()
			mFont = newFont
		End
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		mGui.Render(mFont)
		
		Return False
	End
Private
	Field mFont	: Font
	Field mGui	: Gui
End

Function Main:Int()
	New FontToolApp()
	Return False
End
