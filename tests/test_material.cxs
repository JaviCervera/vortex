Strict

#GLFW_WINDOW_TITLE="Vortex2 Material Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init()
			Print "Error: " + Stats.ShaderError()
			EndApp()
		End
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		Print "Shader compilation: " + Stats.ShaderError()
		
		'Load font
		mFont = Font.Load("system.fnt.dat")
		
		'Set skybox
		World.SetSkyboxTexture(Texture.Load("left.jpg", "right.jpg", "front.jpg", "back.jpg", "top.jpg", "bottom.jpg"))
		
		'Create camera
		mCam = New Camera()
		mCam.ClearMode = Camera.CLEAR_SKYBOX
		
		'Create box
		mModel = New Model(Mesh.Load("cube.msh.dat"))
		'mModel.Material.Shininess = 1
		'mModel.Material.DiffuseTexture = Null
		'mModel.Material.ReflectionTexture = mTexture
		'mModel.Material.RefractionTexture = mTexture
		'mModel.Material.RefractionCoef = 0.98
		
		'Create light attached to camera
		mLight = New Light(mCam)
		mLight.Radius = 10
		
		'Disable sun and setup lighting
		World.SetSunColor(Color.BLACK)
		World.SetAmbient(Color.WHITE)
			
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Rotate camera
		mCam.Yaw += 32 * Stats.DeltaTime()
		mCam.SetPosition(0, 0, 0)
		mCam.Move(0, 0, -3)
		
		'Update shininess
		If KeyHit(KEY_SPACE)
			If ( mModel.Material.Shininess >= 1 )
				mModel.Material.Shininess = 0
			Else
				mModel.Material.Shininess = Clamp(mModel.Material.Shininess + 0.1, 0.0, 1.0)
			End
		End

		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup renderer for 2D graphics
		Renderer.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Renderer.SetColor(Color.WHITE)
		Local text$ = Stats.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Stats.RenderCalls()
		mFont.Draw(2, 18, text)
		
		'Draw shininess
		text = "Shininess: " + String(mModel.Material.Shininess)[..3]
		mFont.Draw(2, 34, text)
	
		Return False
	End
Private
	Field mFont		: Font
	Field mCam		: Camera
	Field mModel	: Model
	Field mLight	: Light
End

Function Main:Int()
	New TestApp()
	Return False
End