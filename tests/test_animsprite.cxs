Strict

#GLFW_WINDOW_TITLE="Vortex2 Sprite Animation Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init() Then EndApp()
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		
		'Load font
		mFont = Font.Load("system.fnt.dat")		
		
		'Load texture
		Local tex:Texture = Texture.Load("robin.png")
		
		'Create camera
		mCam = New Camera()
		mCam.Orthographic = True
		mCam.Z = -1
		mCam.ClearColor = Color.BLACK
		
		'Create sprite
		mSprite = New Sprite(tex, 5, 5)
		mSprite.Size(16, 16)
		mSprite.LastFrame -= 3	'Skip the last three frames in the animation, since they are empty
		mSprite.FPS = 16
		
		'Disable sun
		World.SunColor(Color.BLACK)
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup renderer for 2D graphics
		Renderer.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Renderer.Color(Color.WHITE)
		Local text:String = Stats.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Stats.RenderCalls()
		mFont.Draw(2, 18, text)
		
		'Draw current frame
		text = "Frame: " + mSprite.CurrentFrame
		mFont.Draw(2, 34, text)
	
		Return False
	End
Private
	Field mFont		: Font
	Field mCam		: Camera
	Field mSprite	: Sprite
End

Function Main:Int()
	New TestApp()
	Return False
End
