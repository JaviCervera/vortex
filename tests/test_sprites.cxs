Strict

#GLFW_WINDOW_TITLE="Vortex2 Sprites Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init()
			Print "Error: " + Stats.ShaderError()
			EndApp()
		End
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		Print "Shader compilation: " + Stats.ShaderError()
		
		'Load font
		mFont = Font.Load("system.fnt.dat")		
		
		'Load texture
		Local smileTex:Texture = Texture.Load("smile.png")
		
		'Create camera
		mCam = New Camera()
		mCam.Rotate(45, -45, 0)
		mCam.ClearColor = Color.WHITE
		
		'Create sprites
		Local x:Float = -8, z:Float = -8
		For Local i:Int = 0 Until 64
			Local spr:Sprite = New Sprite(smileTex)
			spr.Position(x, 0, z)
			spr.Size(1, 1)
			spr.Material.Color = Color.RGB(Rnd(256), Rnd(256), Rnd(256))
			x += 2; If x >= 8 Then x = -8; z += 2
		Next
		
		'Disable sun
		World.SunColor(Color.BLACK)
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Increment camera rotation
		mCam.Yaw += 32 * Stats.DeltaTime()
		mCam.Position(0, 0, 0)
		mCam.Move(0, 0, -8)
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup renderer for 2D graphics
		Renderer.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Renderer.SetColor(Color.BLACK)
		Local text$ = Stats.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Stats.RenderCalls()
		mFont.Draw(2, 18, text)
	
		Return False
	End
Private
	Field mFont	: Font
	Field mCam	: Camera
End

Function Main:Int()
	New TestApp()
	Return False
End
