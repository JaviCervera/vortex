Strict

#GLFW_WINDOW_TITLE="Vortex2 Van Gogh"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2
#OPENGL_DEPTH_BUFFER_ENABLED=True
#BINARY_FILES+="*.msh|*.skl|*.anm|*.fnt"
'#GLFW_GCC_MSIZE_WINNT="32"

#If TARGET="glfw" And HOST<>"linux"
Import brl.requesters
#Endif
Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init()
#If TARGET="glfw" And HOST<>"linux"
			Notify "Error", Stats.ShaderError(), True
#Else
			Print "Error: " + Stats.ShaderError()
#Endif
			EndApp()
		End
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		Print "Shader compilation: " + Stats.ShaderError()
		
		'Load font
		mFont = Cache.LoadFont("system.fnt")
		
		'Create camera
		mCam = Camera.Create()
		mCam.SetPosition(0, 1.5, -6)
		
		'Create model
		mModel = Model.Create(Cache.LoadMesh("vangogh.msh"))
		mModel.Yaw = 180
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera view
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Update camera rotation
		mValue += 0.5 * Stats.DeltaTime()
		mCam.Yaw = 30 * Sinr(mValue)
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup renderer for 2D graphics
		Renderer.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw
		Local text:String = "Van Gogh Room by ruslans3d is licensed under CC Attribution"
		Renderer.SetColor(Color.RGB(15, 15, 15, 200))
		Renderer.DrawRect(0, 8, DeviceWidth(), 20)
		Renderer.SetColor(Color.RGB(100, 120, 153))
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, 8, text)
	
		Return False
	End
Private	
	Field mFont				: Font
	Field mCam				: Camera
	Field mModel			: Model
	Field mValue			: Float
End

Function Main:Int()
	New TestApp()
	Return False
End
