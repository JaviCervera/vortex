Strict

#GLFW_WINDOW_TITLE="Vortex2 Lighting Test"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2

Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init()
			Print "Error: " + Stats.ShaderError()
			EndApp()
		End
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		Print "Shader compilation: " + Stats.ShaderError()
		World.SetAmbient(Color.BLACK)
		
		'Load font
		mFont = Cache.LoadFont("system.fnt.dat")
		
		'Load sphere mesh
		Local mesh:Mesh = Cache.LoadMesh("sphere.msh.dat")
		
		'Create camera
		mCam = Camera.Create()
		mCam.ClearColor = Color.BLACK
		mCam.SetPosition(0, 32, -90)
		mCam.SetRotation(16, 0, 0)
		
		'Create sphere models
		Local x:Int = -32, z:Int = -32
		For Local i:Int = 0 Until 81
			Local model:Model = Model.Create(mesh)
			model.SetPosition(x, 0, z)
			x += 8; If x > 32 Then x = -32; z += 8
		Next
		
		'Create lights
		mLights[0] = Light.Create(Light.TYPE_POINT)
		mLights[1] = Light.Create(Light.TYPE_POINT)
		mLights[2] = Light.Create(Light.TYPE_POINT)
		mLights[0].Radius = 48
		mLights[1].Radius = 48
		mLights[2].Radius = 48
		mLights[0].Color = Color.RED
		mLights[1].Color = Color.GREEN
		mLights[2].Color = Color.BLUE
		mLights[0].Yaw = 0
		mLights[1].Yaw = 120
		mLights[2].Yaw = 240
		
		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Update lights
		For Local i:Int = 0 Until 3
			mLights[i].Yaw -= 32 * Stats.DeltaTime()
			mLights[i].SetPosition(0, 0, 0)
			mLights[i].Move(0, 0, -48)
		Next
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup renderer for 2D graphics
		Renderer.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw FPS
		Renderer.SetColor(Color.WHITE)
		Local text$ = Stats.FPS() + " FPS"
		mFont.Draw(2, 2, text)
		
		'Draw RenderCalls
		text = "Render calls: " + Stats.RenderCalls()
		mFont.Draw(2, 18, text)
	
		Return False
	End
Private
	Field mFont		: Font
	Field mCam		: Camera
	Field mLights	: Light[3]
End

Function Main:Int()
	New TestApp()
	Return False
End
