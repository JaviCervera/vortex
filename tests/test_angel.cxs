Strict

#GLFW_WINDOW_TITLE="Vortex2 Angel"
#GLFW_WINDOW_WIDTH=800
#GLFW_WINDOW_HEIGHT=600
#GLFW_WINDOW_RESIZABLE=True
#GLFW_WINDOW_SAMPLES=2
#OPENGL_DEPTH_BUFFER_ENABLED=True
#BINARY_FILES+="*.msh|*.skl|*.anm|*.fnt"
'#GLFW_GCC_MSIZE_WINNT="32"

#If TARGET="glfw" And HOST<>"linux"
Import brl.requesters
#Endif
Import mojo.app
Import mojo.input
Import vortex

Class TestApp Extends App Final
Public
	Method OnCreate:Int()
		'Setup
		SetUpdateRate(0)
		SetSwapInterval(0)
		Seed = Millisecs()
	
		'Init vortex
		If Not World.Init()
#If TARGET="glfw" And HOST<>"linux"
			Notify "Error", Stats.ShaderError(), True
#Else
			Print "Error: " + Stats.ShaderError()
#Endif
			EndApp()
		End
		Print "Vendor name: " + Stats.VendorName()
		Print "Renderer name: " + Stats.RendererName()
		Print "API version name: " + Stats.APIVersionName()
		Print "Shading version name: " + Stats.ShadingVersionName()
		Print "Shader compilation: " + Stats.ShaderError()
		
		'Load font
		mFont = Cache.LoadFont("system.fnt")
		
		'Create camera
		mCam = Camera.Create()
		mCam.SetPosition(7, 7, -7)
		mCam.SetRotation(37.5, -45, 0)
		mCam.ClearColor = Color.RGB(15, 15, 15)
		
		'Create model
		mModel = Model.Create(Cache.LoadMesh("angel.msh"))
		mModel.Yaw = 180.0
		
		'Create lights
		mLights[0] = Light.Create()
		mLights[1] = Light.Create(Light.TYPE_POINT)
		mLights[2] = Light.Create(Light.TYPE_POINT)
		mLights[3] = Light.Create(Light.TYPE_POINT)
		mLights[4] = Light.Create(Light.TYPE_POINT)
		mLights[0].SetRotation(45, -45, 0)
		mLights[1].SetPosition(0, 0, -2)
		mLights[2].SetPosition(2, 0, 0)
		mLights[3].SetPosition(0, 0, 2)
		mLights[4].SetPosition(-2, 0, 0)
		mLights[0].Color = Color.RGBF(0.6, 0.6, 0.6)
		mLights[1].Color = Color.RGBF(0.5, 1, 0.7)
		mLights[2].Color = Color.RGBF(1, 0.7, 0.2)
		mLights[3].Color = Color.RGBF(1, 0.5, 0.7)
		mLights[4].Color = Color.RGBF(0.2, 0.7, 1)

		Return False
	End
	
	Method OnUpdate:Int()	
		'End with escape key
		#If TARGET<>"html5"
		If KeyHit(KEY_ESCAPE) Then EndApp()
		#End
		
		'Update world
		World.Update()
		
		'Update camera
		mCam.AspectRatio = Float(DeviceWidth()) / DeviceHeight()
		mCam.ViewportWidth = DeviceWidth()
		mCam.ViewportHeight = DeviceHeight()
		
		'Update model
		mModel.Yaw += 32 * Stats.DeltaTime()
		
		Return False
	End
	
	Method OnRender:Int()
		'Render world
		World.Render()
		
		'Setup renderer for 2D graphics
		Renderer.Setup2D(0, 0, DeviceWidth(), DeviceHeight())
		
		'Draw
		Local text:String = "Frank by misterdevious is licensed under CC Attribution-NonCommercial-ShareAlike"
		Renderer.SetColor(Color.RGB(240, 240, 240))
		mFont.Draw((DeviceWidth() - mFont.TextWidth(text)) / 2, 8, text)
	
		Return False
	End
Private
	Field mFont				: Font
	Field mCam				: Camera
	Field mModel			: Model
	Field mLights			: Light[5]
	Field mValue			: Float
End

Function Main:Int()
	New TestApp()
	Return False
End
