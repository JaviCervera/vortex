Strict

Private
Import brl.filepath
Import dialog
Import mesh
Import mojo.app
Import mojo.input
Import playniax.pyro.fonts.default14
Import playniax.pyro.fonts.default21
Import playniax.pyro.framework.contentmanager
Import playniax.pyro.engine.gui
Import vortex

Public
Class Gui Final
Public
	Method New()	
		'Load resources
		mCube = Mesh.CreateCube()
		
		'Create gui layer
		mGuiLayer = New prGuiLayer
		mGuiLayer.Renderer.SetClearMode(0)
		vortex.Renderer.SetupMojo()
		
		Local font:prBitmapFont = prContent.GetFont("pyro_fonts_default_14.txt")
		
		'Add cube button
		mCubeButton = New prGuiButton
		mCubeButton.SetLayer(mGuiLayer)
		mCubeButton.SetName("cube_button")
		mCubeButton.Surface.DrawData(prGuiState.Idle).SetImage(prContent.GetImage("cube.png"))
		mCubeButton.Surface.DrawData(prGuiState.Down).SetScale(0.95, 0.95)
		mCubeButton.SetPosition(8 + 4, 8 + 4)
		mCubeButton.Width = 16
		mCubeButton.Height = 16
		
		'Add open button
		mOpenButton = New prGuiButton
		mOpenButton.SetLayer(mGuiLayer)
		mOpenButton.SetName("open_button")
		mOpenButton.Surface.DrawData(prGuiState.Idle).SetImage(prContent.GetImage("folder.png"))
		mOpenButton.Surface.DrawData(prGuiState.Down).SetScale(0.95, 0.95)
		mOpenButton.SetPosition(8 + 24, 8 + 4)
		mOpenButton.Width = 16
		mOpenButton.Height = 16
		
		'Add save button
		mSaveButton = New prGuiButton
		mSaveButton.SetLayer(mGuiLayer)
		mSaveButton.SetName("save_button")
		mSaveButton.Surface.DrawData(prGuiState.Idle).SetImage(prContent.GetImage("disk.png"))
		mSaveButton.Surface.DrawData(prGuiState.Down).SetScale(0.95, 0.95)
		mSaveButton.SetPosition(8 + 44, 8 + 4)
		mSaveButton.Width = 16
		mSaveButton.Height = 16
		
		'Add export animations button
		mAnimationsButton = New prGuiButton
		mAnimationsButton.SetLayer(mGuiLayer)
		mAnimationsButton.SetName("anim_button")
		mAnimationsButton.Label.Text = "Export Animations: Yes"
		mAnimationsButton.Label.SetBitmapFont(font)
		mAnimationsButton.Label.DrawData(prGuiState.Idle).SetColor(0, 0, 0)
		mAnimationsButton.Label.DrawData(prGuiState.Down).SetColor(0, 0, 1)
		mAnimationsButton.Label.DrawData(prGuiState.RollOver).SetColor(0, 0, 1)
		mAnimationsButton.SetPosition(150, 8 + 4)
		mAnimationsButton.SetTextCenter = False
		mAnimationsButton.Enabled = False
		
		'Add material panel
		mMaterialPanel = New prGuiScrollBox(600, 150)
		mMaterialPanel.SetLayer(mGuiLayer)
		mMaterialPanel.SetClearColor(1, 1, 1)
		mMaterialPanel.Enabled = False
		
		'Add selected material label
		mSelMatLabel = New prGuiButton
		mSelMatLabel.SetLayer(mGuiLayer)
		mSelMatLabel.SetGroup(mMaterialPanel)
		mSelMatLabel.SetName("selmat_button")
		mSelMatLabel.Label.Text = "Material"
		mSelMatLabel.Label.SetBitmapFont(font)
		mSelMatLabel.Label.DrawData(prGuiState.Idle).SetColor(0, 0, 0)
		mSelMatLabel.Label.DrawData(prGuiState.Down).SetColor(0, 0, 1)
		mSelMatLabel.Label.DrawData(prGuiState.RollOver).SetColor(0, 0, 1)
		mSelMatLabel.SetPosition(79, 12)
		mSelMatLabel.Width = 150
		mSelMatLabel.Height = 16
		mSelMatLabel.SetTextCenter = False
		
		mExportAnimations = True
		mPitchFix = 0
		mYawFix = 0
		mRollFix = 0
	End
	
	Method Update:Model(currentModel:Model)
		mMaterialPanel.SetPosition(4, DeviceHeight() - mMaterialPanel.Height - 4)
	
		'Update Gui
		mGuiLayer.Update()
		
		'Process events
		If prEvent.Get("cube_button=Released")
			ResetProperties()
			mSaveButton.Enabled = True
			mAnimationsButton.Enabled = False
			mMaterialPanel.Enabled = True
			Return New Model(mCube)
		Elseif prEvent.Get("open_button=Released")
			Local filename:String = RequestFile("Load mesh")', "Mesh Files:msh.dat;All Files:*", False)
			If filename <> ""
				filename = filename.Replace("\", "/")
				Local mesh:Mesh = LoadMesh(filename)
				If mesh <> Null
					ResetProperties()
					mSaveButton.Enabled = True
					mAnimationsButton.Enabled = mesh.NumBones > 0
					mMaterialPanel.Enabled = True
					Return New Model(mesh)
				Else
					Notify "Error", "Could not load mesh '" + filename + "'", True
				End
			End
		Elseif prEvent.Get("save_button=Released") And currentModel
			Local filename:String = currentModel.Mesh.Filename
			If filename = ""
				filename = RequestFile("Save mesh", "Mesh Files:msh.dat;All Files:*", True)
				If filename <> "" Then currentModel.Mesh.Filename = filename
			End
			If filename <> ""
				'Save mesh
				SaveMesh(currentModel.Mesh, StripExt(StripExt(filename)) + ".msh.dat", mExportAnimations And currentModel.Mesh.NumBones > 0)
				
				'Save skeleton and animations
				If mExportAnimations And currentModel.Mesh.NumBones > 0
					SaveSkeleton(currentModel.Mesh, StripExt(StripExt(filename)) + ".skl.dat")
					SaveAnimation(currentModel.Mesh, StripExt(StripExt(filename)) + ".anm.dat")
				End
				
				Notify("Info", "Mesh saved as '" + StripExt(StripExt(filename)) + ".msh.dat'")
			End
		Elseif prEvent.Get("anim_button=Released")
			mExportAnimations = Not mExportAnimations
			If mExportAnimations
				mAnimationsButton.Label.Text = "Export Animations: Yes"
			Else
				mAnimationsButton.Label.Text = "Export Animations: No"
			End
		Elseif prEvent.Get("selmat_button=Released")
			Notify "", ""
		End
	
		Return Null
	End
	
	Method Render:Void(currentModel:Model)
		'Render GUI
		mGuiLayer.SetVirtual(DeviceWidth(), DeviceHeight())
		vortex.Renderer.SetupMojo()
		mGuiLayer.Render()
	End
	
	Method ResetProperties:Void()
		mSelMat = 0
		mPitchFix = 0
		mYawFix = 0
		mRollFix = 0
	End
Private
	'Resources
	Field mCube					: Mesh
	
	'Gui
	Field mGuiLayer					: prGuiLayer
	Field mCubeButton				: prGuiButton
	Field mOpenButton				: prGuiButton
	Field mSaveButton				: prGuiButton
	Field mAnimationsButton	: prGuiButton
	Field mMaterialPanel		: prGuiScrollBox
	Field mSelMatLabel			: prGuiButton
	
	'Logic
	Field mExportAnimations		: Bool
	Field mSelMat				: Int
	Field mPitchFix				: Int
	Field mYawFix				: Int
	Field mRollFix				: Int
End
