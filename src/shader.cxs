Strict

Private
Import color
Import math3d
Import renderer

'#If TARGET="html5"
Const GLSL_VERSION : String = ""
'#Else
'Const GLSL_VERSION:String = "#version 120~n"
'#EndIf

Private

Class ShaderUniforms Final
Public
	Field mMVP:Int
	Field mModelView:Int
	Field mNormalMatrix:Int
	Field mInvView:Int
	Field mDepthBias:Int
	Field mSolidMode:Int
	Field mTextureMatrix:Int
	Field mBaseTexMode:Int
	Field mUseNormalTex:Int
	Field mUseLightmap:Int
	Field mUseReflectTex:Int
	Field mUseRefractTex:Int
	Field mBaseTexSampler:Int
	Field mBaseCubeSampler:Int
	Field mNormalTexSampler:Int
	Field mLightmapSampler:Int
	Field mReflectCubeSampler:Int
	Field mRefractCubeSampler:Int
	Field mDepthSampler:Int
	Field mUsePixelLighting:Int
	Field mNumLights:Int
	Field mLightPos:Int[Renderer.MaxLights()]
	Field mLightColor:Int[Renderer.MaxLights()]
	Field mLightRadius:Int[Renderer.MaxLights()]
	Field mBaseColor:Int
	Field mAmbient:Int
	Field mShininess:Int
	Field mRefractCoef:Int
	Field mFogEnabled:Int
	Field mFogDist:Int
	Field mFogColor:Int
	Field mShadowsEnabled:Int
	Field mDepthEpsilon:Int
	Field mSkinned:Int
	Field mBones:Int[Renderer.MaxBones()]
	
	Method New(shader:Int)
		mMVP = Renderer.ShaderLocation(shader, "mvp")
		mModelView = Renderer.ShaderLocation(shader, "modelView")
		mNormalMatrix = Renderer.ShaderLocation(shader, "normalMatrix")
		mInvView = Renderer.ShaderLocation(shader, "invView")
		mDepthBias = Renderer.ShaderLocation(shader, "depthBias")
		mSolidMode = Renderer.ShaderLocation(shader, "solidMode")
		mTextureMatrix = Renderer.ShaderLocation(shader, "textureMatrix")
		mBaseTexMode = Renderer.ShaderLocation(shader, "baseTexMode")
		mUseNormalTex = Renderer.ShaderLocation(shader, "useNormalTex")
		mUseLightmap = Renderer.ShaderLocation(shader, "useLightmap")
		mUseReflectTex = Renderer.ShaderLocation(shader, "useReflectTex")
		mUseRefractTex = Renderer.ShaderLocation(shader, "useRefractTex")
		mUsePixelLighting = Renderer.ShaderLocation(shader, "usePixelLighting")
		mNumLights = Renderer.ShaderLocation(shader, "numLights")
		For Local i:Int = 0 Until Renderer.MaxLights()
			mLightPos[i] = Renderer.ShaderLocation(shader, "lightPos[" + i + "]")
			mLightColor[i] = Renderer.ShaderLocation(shader, "lightColor[" + i + "]")
			mLightRadius[i] = Renderer.ShaderLocation(shader, "lightRadius[" + i + "]")
		Next
		mBaseColor = Renderer.ShaderLocation(shader, "baseColor")
		mAmbient = Renderer.ShaderLocation(shader, "ambient")
		mShininess = Renderer.ShaderLocation(shader, "shininess")
		mRefractCoef = Renderer.ShaderLocation(shader, "refractCoef")
		mFogEnabled = Renderer.ShaderLocation(shader, "fogEnabled")
		mFogDist = Renderer.ShaderLocation(shader, "fogDist")
		mFogColor = Renderer.ShaderLocation(shader, "fogColor")
		mShadowsEnabled = Renderer.ShaderLocation(shader, "shadowsEnabled")
		mDepthEpsilon = Renderer.ShaderLocation(shader, "depthEpsilon")
		mSkinned = Renderer.ShaderLocation(shader, "skinned")
		For Local i:Int = 0 Until Renderer.MaxBones()
			mBones[i] = Renderer.ShaderLocation(shader, "bones[" + i + "]")
		Next
		mBaseTexSampler = Renderer.ShaderLocation(shader, "baseTexSampler")
		mBaseCubeSampler = Renderer.ShaderLocation(shader, "baseCubeSampler")
		mNormalTexSampler = Renderer.ShaderLocation(shader, "normalTexSampler")
		mLightmapSampler = Renderer.ShaderLocation(shader, "lightmapSampler")
		mReflectCubeSampler = Renderer.ShaderLocation(shader, "reflectCubeSampler")
		mRefractCubeSampler = Renderer.ShaderLocation(shader, "refractCubeSampler")
		mDepthSampler = Renderer.ShaderLocation(shader, "depthSampler")
	End
	
	Method Prepare:Void()
		'Get state
		Local state:RendererState = Renderer.State()
	
		'Calculate ModelView
		If mModelView <> -1 Or mNormalMatrix <> -1
			mTempMatrix.Set(state.ViewMatrix)
			mTempMatrix.Mul(state.ModelMatrix)
			Renderer.SetShaderMat4(mModelView, mTempMatrix)
		End

		'Calculate normal
		If mNormalMatrix <> -1
			mTempMatrix.Invert()
			mTempMatrix.Transpose()
			Renderer.SetShaderMat4(mNormalMatrix, mTempMatrix)
		End
		
		'Set inverse view
		If mInvView <> -1 Then Renderer.SetShaderMat4(mInvView, state.InverseViewMatrix)

		'Calculate MVP
		'If mMVP <> -1
			mTempMatrix.Set(state.ProjectionMatrix)
			mTempMatrix.Mul(state.ViewMatrix)
			mTempMatrix.Mul(state.ModelMatrix)
			Renderer.SetShaderMat4(mMVP, mTempMatrix)
		'End
		
		'Calculate depth bias
		If mDepthBias <> -1
			mTempMatrix.Set(state.DepthBiasMatrix)
			mTempMatrix.Mul(state.ModelMatrix)
			Renderer.SetShaderMat4(mDepthBias, mTempMatrix)
		End
		
		'Set shadow data
		If mShadowsEnabled <> -1 Then Renderer.SetShaderInt(mShadowsEnabled, state.ShadowsEnabled)
		If mDepthEpsilon <> -1 Then Renderer.SetShaderFloat(mDepthEpsilon, state.DepthEpsilon)
		
		'Set animation data
		If mSkinned <> -1 Then Renderer.SetShaderInt(mSkinned, state.Skinned)
		If mBones[0] <> -1
			Local lastIndex:Int = Min(Renderer.MaxBones(), state.BoneMatrices.Length())
			For Local i:Int = 0 Until lastIndex
				If mBones[i] <> -1 Then Renderer.SetShaderMat4(mBones[i], state.BoneMatrices[i])
			Next
		End
		
		'Set solid mode
		If mSolidMode <> -1
			If state.BlendMode = Renderer.BLEND_SOLID Then Renderer.SetShaderInt(mSolidMode, True) Else Renderer.SetShaderInt(mSolidMode, False)
		End
		
		'Set color
		If mBaseColor <> -1 Then Renderer.SetShaderVec4(mBaseColor, Color.Red(state.Color) / 255.0, Color.Green(state.Color) / 255.0, Color.Blue(state.Color) / 255.0, Color.Alpha(state.Color) / 255.0)
		
		'Set ambient
		If mAmbient <> -1 Then Renderer.SetShaderVec3(mAmbient, Color.Red(state.Ambient) / 255.0, Color.Green(state.Ambient) / 255.0, Color.Blue(state.Ambient) / 255.0)
	
		'Set shininess
		If mShininess <> -1 Then Renderer.SetShaderFloat(mShininess, state.Shininess)
		
		'Set fog
		If mFogEnabled <> -1 Then Renderer.SetShaderInt(mFogEnabled, state.FogEnabled)
		If state.FogEnabled
			If mFogDist <> -1 Then Renderer.SetShaderVec2(mFogDist, state.FogMinDistance, state.FogMaxDistance)
			If mFogDist <> -1 Then Renderer.SetShaderVec3(mFogColor, Color.Red(state.FogColor) / 255.0, Color.Green(state.FogColor) / 255.0, Color.Blue(state.FogColor) / 255.0)
		End
		
		'Set refract coef
		If mRefractCoef <> -1 Then Renderer.SetShaderFloat(mRefractCoef, state.RefractCoef)
		
		'Set lighting
		If mUsePixelLighting <> -1 Then Renderer.SetShaderInt(mUsePixelLighting, state.PixelLighting)
		If mNumLights <> -1 Then Renderer.SetShaderInt(mNumLights, state.NumLights)
		For Local i:Int = 0 Until state.NumLights
			If mLightPos[i] <> -1 Then Renderer.SetShaderVec4(mLightPos[i], state.LightPos[i].X, state.LightPos[i].Y, state.LightPos[i].X, state.LightW[i])
			If mLightColor[i] <> -1 Then Renderer.SetShaderVec3(mLightColor[i], Color.Red(state.LightColor[i]) / 255.0, Color.Green(state.LightColor[i]) / 255.0, Color.Blue(state.LightColor[i]) / 255.0)
			If mLightRadius[i] <> -1 Then Renderer.SetShaderFloat(mLightRadius[i], state.LightRadius[i])
		Next
		
		'Set textures
		If mBaseTexSampler <> -1 Then Renderer.SetShaderInt(mBaseTexSampler, Renderer.BASETEX_UNIT)
		If mBaseCubeSampler <> -1 Then Renderer.SetShaderInt(mBaseCubeSampler, Renderer.BASECUBE_UNIT)
		If mNormalTexSampler <> -1 Then Renderer.SetShaderInt(mNormalTexSampler, Renderer.NORMALTEX_UNIT)
		If mLightmapSampler <> -1 Then Renderer.SetShaderInt(mLightmapSampler, Renderer.LIGHTMAP_UNIT)
		If mReflectCubeSampler <> -1 Then Renderer.SetShaderInt(mReflectCubeSampler, Renderer.REFLECTTEX_UNIT)
		If mRefractCubeSampler <> -1 Then Renderer.SetShaderInt(mRefractCubeSampler, Renderer.REFRACTTEX_UNIT)
		If mDepthSampler <> -1 Then Renderer.SetShaderInt(mDepthSampler, Renderer.DEPTHTEX_UNIT)
		If mBaseTexMode <> -1 Then Renderer.SetShaderInt(mBaseTexMode, state.BaseTexMode)
		If mUseNormalTex <> -1 Then Renderer.SetShaderInt(mUseNormalTex, state.UseNormalTex)
		If mUseLightmap <> -1 Then Renderer.SetShaderInt(mUseLightmap, state.UseLightTex)
		If mUseReflectTex <> -1 Then Renderer.SetShaderInt(mUseReflectTex, state.UseReflectTex)
		If mUseRefractTex <> -1 Then Renderer.SetShaderInt(mUseRefractTex, state.UseRefractTex)
	
		'Set texture matrix
		If mTextureMatrix <> -1 Then Renderer.SetShaderMat4(mTextureMatrix, state.TextureMatrix)
	End
Private
	Global mTempMatrix	: Mat4 = Mat4.Create()
End

Class ShaderAttribs Final
Public
	Field mVertexPosition			: Int
	Field mVertexNormal				: Int
	Field mVertexTangent			: Int
	Field mVertexColor				: Int
	Field mVertexTexCoords		: Int
	Field mVertexTexCoords2		: Int
	Field mVertexBoneIndices	: Int
	Field mVertexBoneWeights	: Int
	
	Method New(shader:Int)
		mVertexPosition = Renderer.ShaderAttribLocation(shader, "vpos")
		mVertexNormal = Renderer.ShaderAttribLocation(shader, "vnormal")
		mVertexTangent = Renderer.ShaderAttribLocation(shader, "vtangent")
		mVertexColor = Renderer.ShaderAttribLocation(shader, "vcolor")
		mVertexTexCoords = Renderer.ShaderAttribLocation(shader, "vtex")
		mVertexTexCoords2 = Renderer.ShaderAttribLocation(shader, "vtex2")
		mVertexBoneIndices = Renderer.ShaderAttribLocation(shader, "vboneIndices")
		mVertexBoneWeights = Renderer.ShaderAttribLocation(shader, "vboneWeights")
	End
	
	Method Enable:Void(coordsOffset:Int, normalsOffset:Int, tangentsOffset:Int, colorsOffset:Int, texCoordsOffset:Int, texCoords2Offset:Int, boneIndicesOffset:Int, boneWeightsOffset:Int, stride:Int)
		If coordsOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexPosition, 3, stride, coordsOffset)
		If normalsOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexNormal, 3, stride, normalsOffset)
		If tangentsOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexTangent, 3, stride, tangentsOffset)
		If colorsOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexColor, 4, stride, colorsOffset)
		If texCoordsOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexTexCoords, 2, stride, texCoordsOffset)
		If texCoords2Offset >= 0 Then Renderer.ShaderEnableAttrib(mVertexTexCoords2, 2, stride, texCoords2Offset)
		If boneIndicesOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexBoneIndices, 4, stride, boneIndicesOffset)
		If boneWeightsOffset >= 0 Then Renderer.ShaderEnableAttrib(mVertexBoneWeights, 4, stride, boneWeightsOffset)
	End
	
	Method Disable:Void()
		Renderer.ShaderDisableAttrib(mVertexPosition)
		Renderer.ShaderDisableAttrib(mVertexNormal)
		Renderer.ShaderDisableAttrib(mVertexTangent)
		Renderer.ShaderDisableAttrib(mVertexColor)
		Renderer.ShaderDisableAttrib(mVertexTexCoords)
		Renderer.ShaderDisableAttrib(mVertexTexCoords2)
		Renderer.ShaderDisableAttrib(mVertexBoneIndices)
		Renderer.ShaderDisableAttrib(mVertexBoneWeights)
	End
End

Public

Class Shader Final
Public
	Method New(vertex:String, fragment:String)
		vertex = GLSL_VERSION + "#define MAX_LIGHTS " + Renderer.MaxLights() + "~n#define MAX_BONES " + Renderer.MaxBones() + "~n" + vertex
		fragment = GLSL_VERSION + "#define MAX_LIGHTS " + Renderer.MaxLights() + "~n" + fragment
		mHandle = Renderer.CreateShader(vertex, fragment)
		If mHandle <> 0
			mUniforms = New ShaderUniforms(mHandle)
			mAttribs = New ShaderAttribs(mHandle)
		End
	End
	
	Method Discard:Void()
		If mHandle <> 0 Then Renderer.FreeShader(mHandle)
		mHandle = 0
	End
	
	Method Handle:Int() Property
		Return mHandle
	End
	
	Method Prepare:Void()
		Renderer.UseShader(mHandle)
		mUniforms.Prepare()
	End
	
	Method EnableVertexVars:Void(coordsOffset:Int, normalsOffset:Int, tangentsOffset:Int, colorsOffset:Int, texCoordsOffset:Int, texCoords2Offset:Int, boneIndicesOffset:Int, boneWeightsOffset:Int, stride:Int)
		mAttribs.Enable(coordsOffset, normalsOffset, tangentsOffset, colorsOffset, texCoordsOffset, texCoords2Offset, boneIndicesOffset, boneWeightsOffset, stride)
	End
	
	Method DisableVertexVars:Void()
		mAttribs.Disable()
	End
Private
	Field mHandle			: Int
	Field mUniforms		: ShaderUniforms
	Field mAttribs		: ShaderAttribs
	
	Method New()
	End
End
