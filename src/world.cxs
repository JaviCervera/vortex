Strict

Private

Import cache
Import camera
Import collision
Import color
Import entity
Import framebuffer
Import light
Import listener
Import material
Import math3d
Import mesh
Import renderer
Import shader
Import stats
Import surface
Import texture

Public
Class World Final
	Function Init:Bool(numLights:Int = 4, numBones:Int = 75)
		If numLights <= 0 Then numLights = 1
		If numBones <= 0 Then numBones = 1
		If Renderer.Init(numLights, numBones)
			Cache.Push()
			Stats._Init()
			mSkybox = Mesh.CreateSkybox()
			mSkybox.Material(0).DepthWrite = False
			SetSunRotation(45, -45)
			SetSunColor(Color.WHITE)
			SetAmbient(Color.RGB(75, 75, 75))
			SetFogEnabled(False)
			SetFogMinDistance(600)
			SetFogMaxDistance(1000)
			SetFogColor(Color.BLACK)
			SetSeparateDepthPass(False)
			SetShadows(False)
			Return True
		Else
			Return False
		End
	End
	
	Function Update:Void()
		Stats._UpdateDeltaTime()
		For Local e:Entity = Eachin mEnabledEntities
			e._Update()
		Next
		Listener._Update()
	End
	
	Function Render:Void()
		'Update screen fps
		Stats._UpdateFPS()
		Local numRenderCalls:Int = 0
		
		'Is there sun light?
		Local sunLightEnabled:Bool = False
		If mSunColor <> Color.BLACK Then sunLightEnabled = True
		
		'Get number of lights
		Local numLights:Int = Min(mLights.Count() + sunLightEnabled, Renderer.MaxLights())

		For Local c:Camera = Eachin mCameras
			'If shadows are enabled, setup shadows
			If mShadowsEnabled
				'Find the point the camera is looking at
				QuatSetEuler(c.Pitch, c.Yaw, c.Roll, mTempArr)
				QuatMulVec3(mTempArr[0], mTempArr[1], mTempArr[2], mTempArr[3], 0, 0, mShadowsRange/2, mTempArr)
				Vec3Add(c.WorldX, c.WorldY, c.WorldZ, mTempArr[0], mTempArr[1], mTempArr[2], mTempArr)
				Local lookx:Float = mTempArr[0]
				Local looky:Float = mTempArr[1]
				Local lookz:Float = mTempArr[2]
				
				'Move from that point to the light source
				QuatSetEuler(mSunPitch, mSunYaw, 0, mTempArr)
				QuatMulVec3(mTempArr[0], mTempArr[1], mTempArr[2], mTempArr[3], 0, 0, -mShadowsSunDist, mTempArr)
				Vec3Add(mTempArr[0], mTempArr[1], mTempArr[2], lookx, looky, lookz, mTempArr)
				
				'Setup depth matrices
				Mat4OrthoHeightLH(mShadowsRange, 1, 0, mShadowsSunDist*2, mDepthProj)
				Mat4LookAtLH(mTempArr[0], mTempArr[1], mTempArr[2], lookx, looky, lookz, 0, 1, 0, mDepthView)
			
				'Set framebuffer
				mFramebuffer.Set()
				
				'Setup renderer
				Renderer.Setup3D(0, 0, mFramebuffer.ColorTexture.Width, mFramebuffer.ColorTexture.Height)
				Shader._CurrentDefault(Shader._DefaultShadow())
				Mat4Copy(mDepthProj, Renderer.State().ProjectionMatrix)
				Mat4Copy(mDepthView, Renderer.State().ViewMatrix)
				Renderer.ClearColorBuffer(Color.WHITE)
				Renderer.ClearDepthBuffer()
				
				'Render entities for shadow step
				For Local e:Entity = Eachin mVisibleEntities
					numRenderCalls += e._Render(Material.STEP_SHADOWS)
				Next
				
				'Set screen
				Framebuffer.SetScreen()
			End
			
			'Prepare camera
			c._PrepareForRender()
			
			'Previous depth pass
			Renderer.State().SeparateDepthPass = mSeparateDepthPass
			If mSeparateDepthPass
				Shader._CurrentDefault(Shader._DefaultShadow())
				Renderer.SetDepthWrite(True)
				
				'Render entities to capture depth
				For Local e:Entity = Eachin mVisibleEntities
					numRenderCalls += e._Render(Material.STEP_SOLID)
				Next
				
				Shader._CurrentDefault(Shader._Default3D())
			End
			
			'Set fog and number of lights
			Renderer.State().FogEnabled = mFogEnabled
			Renderer.State().FogColor = mFogColor
			Renderer.State().FogMinDistance = mFogMin
			Renderer.State().FogMaxDistance = mFogMax
			Renderer.State().NumLights = numLights
			
			'Set depth data
			If mShadowsEnabled
				Mat4Identity(Renderer.State().DepthBiasMatrix)
				Renderer.State().DepthBiasMatrix[0] = 0.5
				Renderer.State().DepthBiasMatrix[5] = 0.5
				Renderer.State().DepthBiasMatrix[10] = 0.5
				Renderer.State().DepthBiasMatrix[12] = 0.5
				Renderer.State().DepthBiasMatrix[13] = 0.5
				Renderer.State().DepthBiasMatrix[14] = 0.5
				Mat4Mul(Renderer.State().DepthBiasMatrix, mDepthProj, Renderer.State().DepthBiasMatrix)
				Mat4Mul(Renderer.State().DepthBiasMatrix, mDepthView, Renderer.State().DepthBiasMatrix)
				Renderer.State().ShadowsEnabled = True
				Renderer.State().DepthEpsilon = mShadowsRange / 10000.0
				Renderer.SetDepthTexture(DepthTexture().Handle)
			Else
				Renderer.State().ShadowsEnabled = False
			End
			
			'Prepare lighting
			If numLights > 0
				Renderer.State().Ambient = mAmbient
			End
			If sunLightEnabled
				QuatSetEuler(mSunPitch, mSunYaw, 0, mTempArr)
				QuatMulVec3(mTempArr[0], mTempArr[1], mTempArr[2], mTempArr[3], 0, 0, -1, mTempArr)
				Mat4MulVec4(Renderer.State().ViewMatrix, mTempArr[0], mTempArr[1], mTempArr[2], 0, Renderer.State().LightPos[0])
				Renderer.State().LightColor[0] = mSunColor
			End
			If numLights > sunLightEnabled
				Local i:Int = sunLightEnabled
				For Local l:Light = Eachin mLights
					If i >= numLights Then Exit
					l._PrepareForRender(i)
					i += 1
				Next
			End
			
			'Render all steps
			If mSeparateDepthPass Then Renderer.SetDepthWrite(False)
			For Local step_:Int = Material.STEP_SOLID Until Material.STEP_COUNT
				For Local e:Entity = Eachin mVisibleEntities
					numRenderCalls += e._Render(step_)
				Next
			End
			
			'Set num render calls
			Stats._SetRenderCalls(numRenderCalls)
		End
	End

	Function SkyboxTexture:Texture()
		Return mSkybox.Material(0).ColorTexture
	End
	
	Function SetSkyboxTexture:Void(tex:Texture)
		mSkybox.Material(0).DiscardTextures()
		mSkybox.Material(0).ColorTexture = tex
	End
	
	Function NumEntities:Int()
		Return mEntities.Count()
	End
	
	Function Entity:Entity(index:Int)
		Local i:Int = 0
		Local node:list.Node<Entity> = mEntities.FirstNode()
		While node And i < index
			node = node.NextNode()
			i += 1
		Wend
		If node And i = index Then Return node.Value() Else Return Null
	End
	
	Function FindEntity:Entity(name:String)
		For Local e:Entity = Eachin mEntities
			If e.Name = name Then Return e
		Next
		Return Null
	End
	
	Function SunPitch:Float()
		Return mSunPitch
	End
	
	Function SunYaw:Float()
		Return mSunYaw
	End
	
	Function SetSunRotation:Void(pitch:Float, yaw:Float)
		mSunPitch = pitch
		mSunYaw = yaw
	End
	
	Function SunColor:Int()
		Return mSunColor
	End
	
	Function SetSunColor:Void(color:Int)
		mSunColor = color
	End
	
	Function Ambient:Int()
		Return mAmbient
	End
	
	Function SetAmbient:Void(amb:Int)
		mAmbient = amb
	End
	
	Function FogEnabled:Bool()
		Return mFogEnabled
	End
	
	Function SetFogEnabled:Void(enabled:Bool)
		mFogEnabled = enabled
	End
	
	Function FogMinDistance:Float()
		Return mFogMin
	End
	
	Function SetFogMinDistance:Void(minDist:Float)
		mFogMin = minDist
	End
	
	Function FogMaxDistance:Float()
		Return mFogMax
	End
	
	Function SetFogMaxDistance:Void(maxDist:Float)
		mFogMax = maxDist
	End
	
	Function FogColor:Int()
		Return mFogColor
	End
	
	Function SetFogColor:Void(color:Int)
		mFogColor = color
	End
	
	Function SeparateDepthPass:Bool()
		Return mSeparateDepthPass
	End
	
	Function SetSeparateDepthPass:Void(enable:Bool)
		mSeparateDepthPass = enable
	End
	
	Function Shadows:Bool()
		Return mShadowsEnabled
	End
	
	Function SetShadows:Void(enable:Bool, range:Float = 500, sunDistance:Float = 0, shadowTexSize:Int = 1024)
		If sunDistance = 0 Then sunDistance = range * 0.5
		If shadowTexSize <= 0 Then shadowTexSize = 1024
		If mFramebuffer Then mFramebuffer.Discard()
		mShadowsEnabled = enable
		mShadowsRange = range
		mShadowsSunDist = sunDistance
		shadowTexSize = Min(shadowTexSize, Stats.MaxTextureSize())
		mFramebuffer = New Framebuffer(shadowTexSize, shadowTexSize, True)
	End
	
	Function DepthTexture:Texture()
		If mFramebuffer Then Return mFramebuffer.ColorTexture Else Return Null
	End
	
	Function AddStaticCollisionBox:Void(x:Float, y:Float, z:Float, width:Float, height:Float, depth:Float)
		mColBoxes = mColBoxes.Resize(mColBoxes.Length + 1)
		mColBoxes[mColBoxes.Length - 1] = New Box(x, y, z, x+width, y+height, z+depth)
	End
	
	Function ClearStaticCollisions:Void()
		mColBoxes = New Box[0]
	End
	
	Function _CheckStaticCollision:Bool(x:Float, y:Float, z:Float, sqRadius:Float)
		'Iterate through all collision boxes
		For Local box:Box = Eachin mColBoxes
			If Collision.BoxSphere(box.Min[0], box.Min[1], box.Min[2], box.Max[0], box.Max[1], box.Max[2], x, y, z, sqRadius)
				Return True
			End
		Next
		
		Return False
	End
	
	Function _AddEntity:Void(e:Entity)
		mEntities.AddLast(e)
	End
	
	Function _FreeEntity:Void(e:Entity)
		_EntityNeedsUpdate(e, False)
		'_EntityNeedsPrepareRender(e, False)
		_EntitySetVisible(e, False)
		mEntities.RemoveFirst(e)
	End
	
	Function _AddCamera:Void(c:Camera)
		mCameras.AddLast(c)
	End
	
	Function _FreeCamera:Void(c:Camera)
		mCameras.RemoveFirst(c)
	End
	
	Function _AddLight:Void(l:Light)
		mLights.AddLast(l)
	End
	
	Function _FreeLight:Void(l:Light)
		mLights.RemoveFirst(l)
	End
	
	Function _EntityNeedsUpdate:Void(e:Entity, update:Bool)
		If update
			If Not mEnabledEntities.Contains(e) Then mEnabledEntities.AddLast(e)
		Else
			mEnabledEntities.RemoveFirst(e)
		End
	End
	
	Function _EntitySetVisible:Void(e:Entity, visible:Bool)
		If visible
			If Not mVisibleEntities.Contains(e) Then mVisibleEntities.AddLast(e)
		Else
			mVisibleEntities.RemoveFirst(e)
		End
	End
	
	Function _RenderSkybox:Void(x:Float, y:Float, z:Float)
		Mat4TransformEuler(x, y, z, 0, 0, 0, 10, 10, 10, Renderer.State().ModelMatrix)
		mSkybox.Material(0)._PrepareForRender(Material.STEP_NODEPTHWRITE)
		mSkybox.Surface(0)._Render()
	End
	
	Function _Entities:List<Entity>()
		Return mEntities
	End
Private
	Global mEntities						: List<Entity> = New List<Entity>
	Global mEnabledEntities			: List<Entity> = New List<Entity>
	Global mVisibleEntities			: List<Entity> = New List<Entity>
	Global mCameras							: List<Camera> = New List<Camera>
	Global mLights							: List<Light> = New List<Light>
	Global mSkybox							: Mesh
	Global mSunPitch						: Float
	Global mSunYaw							: Float
	Global mSunColor						: Int
	Global mAmbient							: Int
	Global mFogEnabled					: Bool
	Global mFogMin							: Float
	Global mFogMax							: Float
	Global mFogColor						: Int
	Global mSeparateDepthPass		: Bool
	Global mFramebuffer					: Framebuffer
	Global mShadowsEnabled			: Bool
	Global mShadowsRange				: Float
	Global mShadowsSunDist			: Float
	Global mDepthProj						: Float[16]
	Global mDepthView						: Float[16]
	Global mColBoxes						: Box[0]
	Global mTempArr							: Float[4]
	
	Method New()
	End
End

Private

Class Box	
	Method New()
		mMin = New Float[3]
		mMax = New Float[3]
	End
	
	Method New(x0:Float, y0:Float, z0:Float, x1:Float, y1:Float, z1:Float)
		Self.mMin = [x0, y0, z0]
		Self.mMax = [x1, y1, z1]
	End
	
	Method Min:Float[]() Property
		Return mMin
	End
	
	Method Max:Float[]() Property
		Return mMax
	End
Private
	Field mMin	: Float[]
	Field mMax	: Float[]
End
